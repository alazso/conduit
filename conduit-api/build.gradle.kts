plugins {
    `maven-publish`
}

val paperApiVersion: String by rootProject.extra
val junitVersion: String by rootProject.extra
val assertjVersion: String by rootProject.extra

description = "Conduit public API — economy interfaces, records, results (zero implementation)."

dependencies {
    // Paper API is provided by the running server; the API shades nothing.
    compileOnly("io.papermc.paper:paper-api:$paperApiVersion")

    testImplementation("io.papermc.paper:paper-api:$paperApiVersion")
    testImplementation("org.junit.jupiter:junit-jupiter:$junitVersion")
    testImplementation("org.assertj:assertj-core:$assertjVersion")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

publishing {
    publications {
        create<MavenPublication>("library") {
            from(components["java"])
            pom {
                name.set("Conduit API")
                description.set(project.description)
                url.set("https://github.com/alazso/conduit")
                licenses {
                    license {
                        name.set("MIT License")
                        url.set("https://opensource.org/licenses/MIT")
                    }
                }
            }
        }
    }
    repositories {
        // Self-hosted Reposilite at repo.alaz.so. Reads are anonymous; deploys use
        // a token supplied via gradle properties (local) or env vars (CI).
        maven {
            name = "alazso"
            url = uri(
                if (version.toString().endsWith("SNAPSHOT")) {
                    "https://repo.alaz.so/snapshots"
                } else {
                    "https://repo.alaz.so/releases"
                }
            )
            credentials {
                username = providers.gradleProperty("alazsoUser")
                    .orElse(providers.environmentVariable("ALAZSO_REPO_USER")).orNull
                password = providers.gradleProperty("alazsoToken")
                    .orElse(providers.environmentVariable("ALAZSO_REPO_TOKEN")).orNull
            }
        }
    }
}
